#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(lwtools, 1.0, support@likewisesoftware.com)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE([-Wall -Werror foreign])
AC_CONFIG_HEADERS([include/config.h])

AM_CPPFLAGS=""
AM_CFLAGS=""
AM_LDFLAGS=""

# Platform-specific stuff

MOD_EXT=".so"

case "$host_os:$host_cpu" in
	linux*:i?86)
		AC_DEFINE([__LWI_LINUX__], [], [Define if OS is Linux])
		;;
	linux*:x86_64)
		AC_DEFINE([__LWI_LINUX__], [], [Define if OS is Linux])
		;;
	solaris*:i?86|solaris*:sparc*)
		AC_DEFINE([__LWI_SOLARIS__], [], [Define if OS is Solaris])
		;;
	darwin*:*)
		AC_DEFINE([__LWI_DARWIN__], [], [Define if OS is Darwin])
		;;
	freebsd*:*)
		AC_DEFINE([__LWI_FREEBSD__], [], [Define if OS is FreeBSD])
		;;
	hpux*:*)
                MOD_EXT=".sl"
		AC_DEFINE([__LWI_HP_UX__], [], [Define if OS is HP-UX])
		;;
	aix*:*)
		AC_DEFINE([__LWI_AIX__], [], [Define if OS is AIX])
		;;
esac

AC_SUBST(MOD_EXT)
AC_DEFINE_UNQUOTED([MOD_EXT], ["$MOD_EXT"], [Extension of shared modules])



# lwbase

AC_ARG_WITH([lwbase],
        [AC_HELP_STRING([--with-lwbase=<dir>], [use lwbase located in prefix <dir>])],
        [
        LWBASE_INCLUDES="-I$withval/include"
        LWBASE_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lwbase-includes],
        [AC_HELP_STRING([--with-lwbase-includes=<dir>], [use lwbase includes located in <dir>])],
        [
        LWBASE_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lwbase-libs],
        [AC_HELP_STRING([--with-lwbase-libs=<dir>], [use lwbase libs located in <dir>])],
        [
        LWBASE_LDFLAGS="-L$withval"
        ])


AC_SUBST(LWBASE_INCLUDES)
AC_SUBST(LWBASE_LDFLAGS)
AC_SUBST(LWBASE_LIBS)


# lwadvapi

AC_ARG_WITH([lwadvapi],
        [AC_HELP_STRING([--with-lwadvapi=<dir>], [use lwadvapi located in prefix <dir>])],
        [
                LWADVAPI_INCLUDES="-I$withval/include"
                LWADVAPI_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lwadvapi-includes],
        [AC_HELP_STRING([--with-lwadvapi-includes=<dir>], [use lwadvapi includes located in <dir>])],
        [
                LWADVAPI_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lwadvapi-libs],
        [AC_HELP_STRING([--with-lwadvapi-libs=<dir>], [use lwadvapi libs located in <dir>])],
        [
                LWADVAPI_LDFLAGS="-L$withval"
        ])

LWADVAPI_LIBS="-llwadvapi"
LWADVAPI_NOTHR_LIBS="-llwadvapi_nothr"

AC_SUBST(LWADVAPI_INCLUDES)
AC_SUBST(LWADVAPI_LDFLAGS)
AC_SUBST(LWADVAPI_LIBS)
AC_SUBST(LWADVAPI_NOTHR_LIBS)

# krb5

AC_ARG_WITH([krb5],
        [AC_HELP_STRING([--with-krb5=<dir>], [use krb5 located in prefix <dir>])],
        [
		KRB5_INCLUDES="-I$withval/include"
        KRB5_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([krb5-includes],
        [AC_HELP_STRING([--with-krb5-includes=<dir>], [use krb5 includes located in <dir>])],
        [
		KRB5_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([krb5-libs],
        [AC_HELP_STRING([--with-krb5-libs=<dir>], [use krb5 libs located in <dir>])],
        [
		KRB5_LDFLAGS="-L$withval"
        ])

KRB5_LIBS="-lgssapi_krb5 -lkrb5 -lk5crypto"

AC_SUBST(KRB5_INCLUDES)
AC_SUBST(KRB5_LDFLAGS)
AC_SUBST(KRB5_LIBS)

# lwioclient
AC_ARG_WITH([lwioclient],
        [AC_HELP_STRING([--with-lwioclient=<dir>], [use lwioclient located in prefix <dir>])],
        [
        LWIO_INCLUDES="-I$withval/include"
        LWIO_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lwioclient-includes],
        [AC_HELP_STRING([--with-lwioclient-includes=<dir>], [use lwioclient includes located in <dir>])],
        [
        LWIO_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lwioclient-libs],
        [AC_HELP_STRING([--with-lwioclient-libs=<dir>], [use lwioclient libs located in <dir>])],
        [
        LWIO_LDFLAGS="-L$withval"
        ])

LWIO_LIBS="-llwioclient"

AC_SUBST(LWIO_INCLUDES)
AC_SUBST(LWIO_LDFLAGS)
AC_SUBST(LWIO_LIBS)

# lwnetapi

AC_ARG_WITH([lwnetapi],
        [AC_HELP_STRING([--with-lwnetapi=<dir>], [use lwnetapi located in prefix <dir>])],
        [
        LWNETAPI_INCLUDES="-I$withval/include"
        LWNETAPI_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lwnetapi-includes],
        [AC_HELP_STRING([--with-lwnetapi-includes=<dir>], [use lwnetapi includes located in <dir>])],
        [
        LWNETAPI_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lwnetapi-libs],
        [AC_HELP_STRING([--with-lwnetapi-libs=<dir>], [use lwnetapi libs located in <dir>])],
        [
        LWNETAPI_LDFLAGS="-L$withval"
        ])

LWNETAPI_LIBS=-llwnetapi

AC_SUBST(LWNETAPI_INCLUDES)
AC_SUBST(LWNETAPI_LDFLAGS)
AC_SUBST(LWNETAPI_LIBS)


# compat
ENABLE_COMPAT=false

AC_ARG_ENABLE([compat],
        [AC_HELP_STRING([--enable-compat], [enable compat-only build (default: disabled)])],
        [
                if test x"$enableval" = x"yes"
                then
			ENABLE_COMPAT=true
		else
			ENABLE_COMPAT=false
                fi
        ])

AM_CONDITIONAL(ENABLE_COMPAT, [$ENABLE_COMPAT])

# unicode

AC_ARG_ENABLE([unicode],
               [AC_HELP_STRING([--disable-unicode], [disable unicode support (default: enabled)])],[use_unicode=$enableval],[use_unicode=yes])

if test x"$use_unicode" = x"yes"
then
    AM_CFLAGS="$AM_CFLAGS -DUNICODE"
fi

# debugging

AC_ARG_ENABLE([debug],
        [AC_HELP_STRING([--enable-debug], [enable debugging (default: disabled)])],
        [
                if test x"$enableval" = x"yes"
                then
			AM_CFLAGS="$AM_CFLAGS -g -O0"
			AM_CPPFLAGS="$AM_CPPFLAGS -DDEBUG"			
                fi
        ])
        
CPPFLAGS="$CPPFLAGS -D_REENTRANT -D_GNU_SOURCE"

AM_CPPFLAGS="$AM_CPPFLAGS -I\$(top_srcdir)/include -I\$(top_builddir)/include"
AM_CFLAGS="$AM_CFLAGS -Wall -Werror -fno-strict-aliasing"

AC_SUBST(AM_CPPFLAGS)
AC_SUBST(AM_CFLAGS)

AS_AC_EXPAND(LIBDIR, $libdir)
AC_DEFINE_UNQUOTED(LIBDIR, "$LIBDIR", [Library directory])

AC_C_BIGENDIAN

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LIBTOOL

AC_CHECK_LIB([nsl], [gethostname], [NSL_LIBS="-lnsl"])
AC_CHECK_LIB([dl], [dlopen], [DL_LIBS="-ldl"])
AC_CHECK_LIB([resolv], [res_query], [RESOLV_LIBS="-lresolv"])
AC_CHECK_LIB([resolv], [__res_query], [RESOLV_LIBS="-lresolv"])
AC_CHECK_LIB([pthread], [pthread_self], [PTHREAD_LIBS="-lpthread"])
AC_CHECK_LIB([rt], [clock_settime], [RT_LIBS="-lrt"])
AC_CHECK_LIB([uuid], [uuid_copy], [UUID_LIBS="-luuid"])
AC_CHECK_LIB([socket], [bind], [SOCKET_LIBS="-lsocket"])
#AC_CHECK_LIB([lwnetapi], [NetApiBufferFree], [LWNETAPI_LIBS="-llwnetapi"])


AC_SUBST(NSL_LIBS)
AC_SUBST(DL_LIBS)
AC_SUBST(RESOLV_LIBS)
AC_SUBST(PTHREAD_LIBS)
AC_SUBST(RT_LIBS)
AC_SUBST(UUID_LIBS)
AC_SUBST(SOCKET_LIBS)

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([inttypes.h arpa/inet.h fcntl.h limits.h netdb.h netinet/in.h])
AC_CHECK_HEADERS([stdint.h stdlib.h stdbool.h string.h strings.h sys/socket.h])
AC_CHECK_HEADERS([syslog.h unistd.h sys/types.h fcntl.h iconv.h sys/stat.h])
AC_CHECK_HEADERS([time.h sys/time.h nss.h nss_common.h nsswitch.h synch.h])
AC_CHECK_HEADERS([pthread.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_HEADER_TIME
AC_STRUCT_TM

# Check for basic types
AC_CHECK_TYPES([int8, int16, int32, int64], [], [],
[#include <sys/types.h>
 #if __ia64
 #include <model.h>
 #endif])
AC_CHECK_TYPES([uint8, uint16, uint32, uint64])

AC_CHECK_SIZEOF([long long int])
AC_CHECK_SIZEOF([long int])

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_FUNC_STRERROR_R
AC_CHECK_FUNCS([atexit bzero localtime_r memset select socket strchr strerror])
AC_CHECK_FUNCS([vsyslog rpl_realloc rpl_malloc clock_gettime clock_settime])
AC_CHECK_FUNCS([settimeofday gettimeofday timegm getgrouplist])
AC_CHECK_FUNCS([strtoll __strtoll strtoull __strtoull strtol strtoul])

AC_CHECK_DECLS([isblank], [], [], [#include <ctype.h>])

AC_CONFIG_FILES([Makefile
                 net/Makefile
                 net/common/Makefile
                 net/driver/Makefile
                 net/file/Makefile
                 net/session/Makefile
                 net/share/Makefile])

AC_OUTPUT
