#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(domainjoin, 1.0, support@likewisesoftware.com)
AM_INIT_AUTOMAKE([-Wall foreign])
AC_CONFIG_HEADER([include/DomainJoinConfig.h])
AC_CONFIG_MACRO_DIR([m4])

RM='/bin/rm -f'
AC_SUBST(RM)

LDSHFLAGS="${LDSHFLAGS} -shared -fPIC"
AC_SUBST(LDSHFLAGS)

AC_ARG_WITH([libxml2],
        [AC_HELP_STRING([--without-libxml2], [Do not use libxml2 (will disable ESX firewall editing)])],
        [],
        [with_libxml2=yes]
)

AC_ARG_WITH(
        libxml2-dir,
        [  --with-libxml2-dir=<prefix>     look for libxml2 headers/libraries in <prefix>],
        [
            LIBXML2_INCLUDE="-I$withval/include -I$withval/include/libxml2"
            LIBXML2_LDFLAGS=-L$withval/lib
            LIBXML2_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        libxml2-libdir,
        [  --with-libxml2-libdir=<libdir>     look for libxml2 libraries in <libdir>],
        [
            LIBXML2_LDFLAGS=-L$withval
            LIBXML2_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        centutils-dir,
        [  --with-centutils-dir=<prefix>     look for centutils headers/libraries in <prefix>],
        [
            CENTUTILS_INCLUDE=-I$withval/include
            CENTUTILS_LDFLAGS=-L$withval/lib
            CENTUTILS_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        centutils-libdir,
        [  --with-centutils-libdir=<libdir>     look for centutils libraries in <libdir>],
        [
            CENTUTILS_LDFLAGS=-L$withval
            CENTUTILS_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        krb5-dir,
        [  --with-krb5-dir=<prefix>     look for krb5 headers/libraries in <prefix>],
        [
            KRB5_INCLUDE=-I$withval/include
            KRB5_LDFLAGS=-L$withval/lib
            KRB5_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        krb5-libdir,
        [  --with-krb5-libdir=<libdir>     look for krb5 libraries in <libdir>],
        [
            KRB5_LDFLAGS=-L$withval
            KRB5_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        openldap-dir,
        [  --with-openldap-dir=<prefix>     look for openldap headers/libraries in <prefix>],
        [
            OPENLDAP_INCLUDE=-I$withval/include
            OPENLDAP_LDFLAGS=-L$withval/lib
            OPENLDAP_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        openldap-libdir,
        [  --with-openldap-libdir=<libdir>     look for openldap libraries in <libdir>],
        [
            OPENLDAP_LDFLAGS=-L$withval
            OPENLDAP_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        netlogon-dir,
        [  --with-netlogon-dir=<prefix>     look for netlogon headers/libraries in <prefix>],
        [
            NETLOGON_INCLUDE=-I$withval/include
            NETLOGON_LDFLAGS=-L$withval/lib
            NETLOGON_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        netlogon-libdir,
        [  --with-netlogon-libdir=<libdir>     look for netlogon libraries in <libdir>],
        [
            NETLOGON_LDFLAGS=-L$withval
            NETLOGON_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        dcerpc-dir,
        [  --with-dcerpc-dir=<prefix>     look for dcerpc headers/libraries in <prefix>],
        [
            DCERPC_INCLUDE=-I$withval/include
            DCERPC_LDFLAGS=-L$withval/lib
            DCERPC_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        dcerpc-libdir,
        [  --with-dcerpc-libdir=<libdir>     look for dcerpc libraries in <libdir>],
        [
            DCERPC_LDFLAGS=-L$withval
            DCERPC_LIBDIR=$withval
        ]
)

AC_ARG_WITH(
        lwdns-dir,
        [  --with-lwdns-dir=<prefix>     look for lwdns headers/libraries in <prefix>],
        [
            LWDNS_INCLUDE=-I$withval/include
            LWDNS_LDFLAGS=-L$withval/lib
            LWDNS_LIBDIR=$withval/lib
        ]
)

AC_ARG_WITH(
        lwdns-libdir,
        [  --with-lwdns-libdir=<libdir>     look for lwdns libraries in <libdir>],
        [
            LWDNS_LDFLAGS=-L$withval
            LWDNS_LIBDIR=$withval
        ]
)

# eventlog

AC_ARG_WITH([eventlog],
        [AC_HELP_STRING([--with-eventlog=<dir>], [use eventlog located in prefix <dir>])],
        [       
                EVENTLOG_INCLUDES="-I$withval/include"
                EVENTLOG_LDFLAGS="-L$withval/lib"
        ])      
                
AC_ARG_WITH([eventlog-includes],
        [AC_HELP_STRING([--with-eventlog-includes=<dir>], [use eventlog includes located in <dir>])],
        [       
                EVENTLOG_INCLUDES="-I$withval"
        ])      
                
AC_ARG_WITH([eventlog-libs],
        [AC_HELP_STRING([--with-eventlog-libs=<dir>], [use eventlog libs located in <dir>])],
        [
                EVENTLOG_LDFLAGS="-L$withval"
        ])      
                
EVENTLOG_LIBS="-leventlog"
                
AC_SUBST(EVENTLOG_INCLUDES)
AC_SUBST(EVENTLOG_LDFLAGS)
AC_SUBST(EVENTLOG_LIBS)

# lwreg

AC_ARG_WITH([lwreg],
        [AC_HELP_STRING([--with-lwreg=<dir>], [use lwreg located in prefix <dir>])],
        [
		LWREG_INCLUDES="-I$withval/include"
        LWREG_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lwreg-includes],
        [AC_HELP_STRING([--with-lwreg-includes=<dir>], [use lwreg includes located in <dir>])],
        [
		LWREG_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lwreg-libs],
        [AC_HELP_STRING([--with-lwreg-libs=<dir>], [use lwreg libs located in <dir>])],
        [
		LWREG_LDFLAGS="-L$withval"
        ])

LWREG_LIBS=-lregclient

AC_CHECK_HEADER([reg/reg.h],,[AC_MSG_ERROR([Could not find lwreg headers])],
[[#include <stdarg.h>
]])
AC_SUBST(LWREG_INCLUDES)
AC_SUBST(LWREG_LDFLAGS)
AC_SUBST(LWREG_LIBS)

# lsass

AC_ARG_WITH([lsass],
        [AC_HELP_STRING([--with-lsass=<dir>], [use lsass located in prefix <dir>])],
        [
		LSASS_INCLUDES="-I$withval/include"
                LSASS_LDFLAGS="-L$withval/lib"
        ])

AC_ARG_WITH([lsass-includes],
        [AC_HELP_STRING([--with-lsass-includes=<dir>], [use lsass includes located in <dir>])],
        [
		LSASS_INCLUDES="-I$withval"
        ])

AC_ARG_WITH([lsass-libs],
        [AC_HELP_STRING([--with-lsass-libs=<dir>], [use lsass libs located in <dir>])],
        [
		LSASS_LDFLAGS="-L$withval"
        ])

# minimal

AC_ARG_WITH([version], [AC_HELP_STRING([--with-version], [build version])],
                       [], [with_version="0.0.0"])

AC_ARG_ENABLE([minimal], [AC_HELP_STRING([--enable-minimal], [build minimal domain join])])
AM_CONDITIONAL(ENABLE_MINIMAL, test x"$enable_minimal" = x"yes")
if test x"$enable_minimal" = x"yes"
then
        AC_DEFINE([ENABLE_MINIMAL], [], [Define if should build minimal domain join])
        AC_DEFINE_UNQUOTED(BUILD_VERSION, ["$with_version"], [build version])
fi

AC_CHECK_LIB(iconv, iconv)

LIBPTHREAD=""
AC_CHECK_LIB([pthread], [pthread_create], [LIBPTHREAD="-lpthread"])

LIBXML2_LIBS=""
case `uname -s` in

    Darwin)

        AS_IF([test "x$with_libxml2" != xno],[
        AC_CHECK_LIB([xml2], [xmlParseFile], [LIBXML2_LIBS="-lxml2 -lm"], AC_MSG_ERROR([Use --with-libxml2-dir to find libxml2.so]), [$LIBXML2_LDFLAGS -lxml2 -lm $LIBPTHREAD])
        AC_DEFINE([HAVE_LIBXML2], [1], [Define to 1 if you have the `xml2' library (-lxml2).])
])

	;;

    *)

	;;
esac

AC_SUBST(LIBXML2_INCLUDE)
AC_SUBST(LIBXML2_LDFLAGS)
AC_SUBST(LIBXML2_LIBS)

LIBRT=""
AC_CHECK_LIB([rt], [clock_settime], [LIBRT="-lrt"])

CENTUTILS_LIBS=""
AC_CHECK_LIB([centutils], [CTOpenFile], [CENTUTILS_LIBS="-lcentutils"], AC_MSG_ERROR([Use --with-centutils-dir to find libcentutils.so]), [$CENTUTILS_LDFLAGS $LIBRT])

AC_SUBST(CENTUTILS_INCLUDE)
AC_SUBST(CENTUTILS_LDFLAGS)
AC_SUBST(CENTUTILS_LIBS)

KRB5_LIBS=""
AC_CHECK_LIB([krb5support], [krb5int_free_error], [KRB5_LIBS="-lkrb5support"], AC_MSG_ERROR([Use --with-krb5-dir to find libkrb5support.so]), [$KRB5_LDFLAGS])
AC_CHECK_LIB([k5crypto], [krb5_c_string_to_key], [KRB5_LIBS="$KRB5_LIBS -lk5crypto"], AC_MSG_ERROR([Use --with-krb5-dir to find libk5crypto.so]), [$KRB5_LDFLAGS $KRB5_LIBS])
AC_CHECK_LIB([krb5], [krb5_init_context], [KRB5_LIBS="$KRB5_LIBS -lk5crypto -lkrb5 -lcom_err"], AC_MSG_ERROR([Use --with-krb5-dir to find libkrb5.so]), [$KRB5_LDFLAGS $KRB5_LIBS -lcom_err])
AC_CHECK_LIB([gssapi_krb5], [gss_init_sec_context], [KRB5_LIBS="-lgssapi_krb5 $KRB5_LIBS"], AC_MSG_ERROR([Use --with-krb5-dir to find libgssapi_krb5.so]), [$KRB5_LDFLAGS $KRB5_LIBS])

AC_SUBST(KRB5_INCLUDE)
AC_SUBST(KRB5_LDFLAGS)
AC_SUBST(KRB5_LIBS)

OPENLDAP_LIBS=""
AC_CHECK_LIB([ldap_r], [ldap_bind], [OPENLDAP_LIBS="-lldap_r -llber"], AC_MSG_ERROR([Use --with-openldap-dir to find libldap.so]), [$OPENLDAP_LDFLAGS -llber $KRB5_LDFLAGS $KRB5_LIBS -lpthread])

AC_SUBST(OPENLDAP_INCLUDE)
AC_SUBST(OPENLDAP_LDFLAGS)
AC_SUBST(OPENLDAP_LIBS)

NETLOGON_LIBS=""
AC_CHECK_LIB([lwnetclientapi], [LWNetGetDCName], [NETLOGON_LIBS="-llwnetclientapi"], AC_MSG_ERROR([Use --with-netlogon-dir to find liblwnetclientapi.so]), [$NETLOGON_LDFLAGS])

AC_SUBST(NETLOGON_INCLUDE)
AC_SUBST(NETLOGON_LDFLAGS)
AC_SUBST(NETLOGON_LIBS)

DCERPC_LIBS=""
AC_CHECK_LIB([dcerpc], [rpc__init], [DCERPC_LIBS="-ldcerpc"], AC_MSG_ERROR([Use --with-dcerpc-dir to find libdcerpc.so]), [$DCERPC_LDFLAGS $KRB5_LDFLAGS $KRB5_LIBS])

AC_SUBST(DCERPC_INCLUDE)
AC_SUBST(DCERPC_LDFLAGS)
AC_SUBST(DCERPC_LIBS)

LWDNS_LIBS=""
AC_CHECK_LIB([lwdns], [DNSUpdateSecure], [LWDNS_LIBS="-llwdns"], AC_MSG_ERROR([Use --with-lwdns-dir to find lwdns libraries]), [$LWDNS_LDFLAGS])

AC_SUBST(LWDNS_INCLUDE)
AC_SUBST(LWDNS_LDFLAGS)
AC_SUBST(LWDNS_LIBS)


LSASS_LIBS=""
AC_CHECK_LIB([lsaclient], [LsaOpenServer], [LSASS_LIBS="$LSASS_LIBS -llsaclient"])
AC_SUBST(LSASS_INCLUDE)
AC_SUBST(LSASS_LDFLAGS)
AC_SUBST(LSASS_LIBS)

CF_FRAMEWORK_LDFLAGS=""
AC_MSG_CHECKING([for -framework CoreFoundation])
old_LDFLAGS="$LDFLAGS"
LDFLAGS="$LDFLAGS -framework CoreFoundation"
AC_TRY_LINK(, , [AC_MSG_RESULT([yes])
		CF_FRAMEWORK_LDFLAGS="-framework CoreFoundation"],
		[AC_MSG_RESULT(no)])
LDFLAGS="$old_LDFLAGS"
AC_SUBST(CF_FRAMEWORK_LDFLAGS)

CPPFLAGS="$CPPFLAGS -D_REENTRANT -fPIC"
LIBVER_MAJOR=1
SHLIBEXT=.so
SHLIBVEXT=.so.$LIBVER_MAJOR
DYNLIBEXT=.so
DYNLIBVEXT=.so.$LIBVER_MAJOR
LWI_OS_FLAGS=""
LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS=""
case `uname -s` in
    AIX)
	LWI_OS_FLAGS="-D__LWI_AIX__"
	LDFLAGS="${LDFLAGS} -static-libgcc"
	BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-o$@'
	BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
	LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    Darwin)
        LWI_OS_FLAGS="-D__LWI_MACOSX__"
        SHLIBEXT=.dylib
        SHLIBVEXT=.$LIBVER_MAJOR.dylib
        LDFLAGS="-headerpad_max_install_names $LDFLAGS"
        BUILD_DYNLIB_CMD='$(CC) -bundle $(CFLAGS) -o $@ $(OBJS) $(LDFLAGS) $(LIBS)'
        BUILD_SHLIB_CMD='$(CC) -undefined error -dynamiclib -compatibility_version $(LIBVER_MAJOR) -current_version $(LIBVER_MAJOR) -install_name "lib$(LIBNAME)$(SHLIBVEXT)" -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS)'
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_mac.o"
        ;;

    Linux)
        LWI_OS_FLAGS="-D__LWI_LINUX__"
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    FreeBSD|"Isilon OneFS")
        LWI_OS_FLAGS="-D__LWI_FREEBSD__"
        case `uname -r` in
            7.*-RELEASE)
                AC_DEFINE([__LWI_FREEBSD7__], [], [Define if OS is FreeBSD 7.x])
            ;;
        esac
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    NetBSD)
        LWI_OS_FLAGS="-D__LWI_NETBSD__"
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    SunOS)
        LWI_OS_FLAGS="-D__LWI_SOLARIS__"
        LDFLAGS="${LDFLAGS} -static-libgcc"
        BUILD_SHLIB_CMD='$(CC) -shared -fPIC -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-o$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
        ;;

    HP-UX)
        LWI_OS_FLAGS="-D__LWI_HP_UX__"
        if test `uname -m` != "ia64"; then
           SHLIBEXT=.sl
           SHLIBVEXT=.sl.$LIBVER_MAJOR
           DYNLIBEXT=.sl
           DYNLIBVEXT=.sl.$LIBVER_MAJOR
        fi
        BUILD_SHLIB_CMD='$(CC) -shared -o $@ $(OBJS) $(LDFLAGS) $(LIBS) -Wl,-h$@'
        BUILD_DYNLIB_CMD=$BUILD_SHLIB_CMD
        LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS="djdaemonmgr_nonmac.o"
	LIB_XNET="-lxnet"
	;;

    *)
        AC_MSG_ERROR(OS not supported, 1)
        ;;
esac

AC_SUBST(SHLIBEXT)
AC_SUBST(SHLIBVEXT)
AC_SUBST(DYNLIBEXT)
AC_SUBST(DYNLIBVEXT)
AC_SUBST(LIBVER_MAJOR)
AC_SUBST(BUILD_SHLIB_CMD)
AC_SUBST(BUILD_DYNLIB_CMD)
AC_SUBST(LWI_OS_FLAGS)
AC_SUBST(LIBDOMAINJOIN_OS_SPECIFIC_OBJECTS)
AC_SUBST(LIB_XNET)

AC_ARG_ENABLE(debug,
[ --enable-debug enable debugging ],
CFLAGS="${CFLAGS} -g"
)

if test "x$CFLAGS" = "x"; then
    CFLAGS="-O2"
fi

if test "x$prefix" = "xNONE"; then
AC_DEFINE_UNQUOTED(PREFIXDIR, "$ac_default_prefix", [The directory that contains centeris/bin])
else
AC_DEFINE_UNQUOTED(PREFIXDIR, "$prefix", [The directory that contains centeris/bin])
fi

AS_AC_EXPAND(LIBDIR, $libdir)
AC_DEFINE_UNQUOTED(LIBDIR, "$LIBDIR", [Library directory])

AC_DEFINE_UNQUOTED(SAMBACONFDIR, "/etc/samba", [The directory that stores lwiauthd.conf])

AC_C_BIGENDIAN([AC_DEFINE(WORDS_BIGENDIAN, 1, [Define to 1 if the system uses bigendian byte ordering])],[AC_DEFINE(WORDS_LITTLEENDIAN, 1, [Define to 1 if the system uses little-endian byte ordering])],[AC_DEFINE(WORDS_UNKNOWN, 1, [Define to 1 if the byte ordering of the system is unknown])])

AC_MSG_CHECKING([LDFLAGS])
AC_MSG_RESULT($LDFLAGS)
# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_LN_S

# Checks for libraries.

AC_CHECK_LIB([nsl], [gethostname])
AC_CHECK_LIB([resolv], [res_query])
AC_CHECK_LIB([pthread], [pthread_create])
AC_CHECK_LIB([rt], [clock_settime])
AC_CHECK_LIB([socket], [bind])
AC_CHECK_LIB([dl], [dlopen])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h limits.h netdb.h netinet/in.h stdlib.h string sys/socket.h syslog.h unistd.h sys/types.h fcntl.h sys/stat.h sys/systeminfo.h time.h sys/time.h CoreFoundation/CoreFoundation.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_HEADER_TIME
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([atexit bzero localtime_r memset select socket strchr strerror vsyslog sysinfo sysconf clock_gettime clock_settime settimeofday gettimeofday sigprocmask sethostname])
AC_CHECK_DECLS([isblank], [], [], [#include <ctype.h>])
AC_CHECK_DECLS([sethostname], [], [], [#include <unistd.h>])


AC_ARG_WITH(gtk,
[ --with-gtk=yes/no     Enable/disable GTK support (default: yes) ],
[ case "$withval" in
no)
	WANT_GTK=no
	;;
*)
	WANT_GTK=yes
	;;
esac
])

if test "x$WANT_GTK" = "xyes"
then
	PKG_CHECK_MODULES(GTK, gtk+-2.0 libglade-2.0 gthread-2.0,have_gtk=yes,have_gtk=no)
fi

AM_CONDITIONAL([BUILD_GTK], [test "x$have_gtk" = "xyes"])

LIBDOMAINJOIN_LDFLAGS="$KRB5_LDFLAGS $OPENLDAP_LDFLAGS $LIBXML2_LDFLAGS $CENTUTILS_LDFLAGS $NETLOGON_LDFLAGS $DCERPC_LDFLAGS"

LIBDOMAINJOIN_LIBS="$LIBXML2_LIBS $CENTUTILS_LIBS $NETLOGON_LIBS $DCERPC_LIBS $KRB5_LIBS" 

AC_SUBST(LIBDOMAINJOIN_LDFLAGS)
AC_SUBST(LIBDOMAINJOIN_LIBS)

m4_foreach([name], [[bindir], [libexecdir]], [dnl
    AS_AC_EXPAND([expanded_]name, ["$]name["])
])

AC_CONFIG_FILES([Makefile
                 libdomainjoin/Makefile
                 domainjoin-cli/src/Makefile
		 domainjoin-gui/gtk/Makefile
		 scripts/ConfigureLogin
		 scripts/gpcron
                 tests/Makefile
                 tests/test_dhcpname_change/src/Makefile
                 tests/test_sec_user/src/Makefile
                 tests/test_login_cfg/src/Makefile
                 tests/test_file_utils/src/Makefile
                 tests/test_join_api/src/Makefile])

case `uname -s` in
    Darwin)
          AC_CONFIG_FILES([domainjoin-gui/carbon/libdomainjoin-mac/src/Makefile])
          ;;
    *)
          ;;
esac

AM_CONDITIONAL([BUILD_LIBDOMINAJOINMAC], [test "x`uname -s`" = "xDarwin"])
AM_CONDITIONAL([BUILD_MACCODE], [test "x`uname -s`" = "xDarwin"])

AS_AC_EXPAND(PREFIXDIR, ["$prefix"])
AC_DEFINE_UNQUOTED(PREFIXDIR, ["$PREFIXDIR"], [Prefix directory])

AS_AC_EXPAND(BINDIR, ["$bindir"])
AC_DEFINE_UNQUOTED(BINDIR, ["$BINDIR"], [Bin directory])

AS_AC_EXPAND(SBINDIR, ["$sbindir"])
AC_DEFINE_UNQUOTED(SBINDIR, ["$SBINDIR"], [Sbin directory])

AS_AC_EXPAND(LOCALSTATEDIR, ["$localstatedir"])
AC_DEFINE_UNQUOTED(LOCALSTATEDIR, ["$LOCALSTATEDIR"], [Var directory])

AC_OUTPUT
